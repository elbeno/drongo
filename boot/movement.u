#ifndef _DROGON_H_
#include "drogon.h"
#endif

func @._moveply			/* XW	Move #actor and look		*/
{
  #actor.location.contents = listdrop (#actor.location.contents, #actor) ;
  $1.contents = listappend($1.contents,#actor) ;
  #actor.location = $1 ;
  #actor.fighting = NULL;
  foreach $pl in (@.onlinePlayers)
    $pl.followers=listdrop($pl.followers, #actor);
  #actor.following=NULL;

  @._quickLook(); /* Do a look, taking note of verbose flag */
} chmod (&@._moveply,"W:rs") ;

func @.__move			/* X	Change an object's location	*/
{
  $1.location.contents = listdrop ($1.location.contents, $1) ; 
  $2.contents = listappend($2.contents,$1) ;
  $1.location = $2 ;
} /* chmod(&@.__move, "W:rs"); - don't let anyone move anything */

func @._move			/* dW   Move, updating player stats     */
{
  $srcloc=$1.location;
  $dstloc=$2;

  if ($1.widget && $2.room && ($2!=#3) && ($2!=#4))
    $1.restoreTime=time()+3600; /* Restore all dropped items after 1 hour */

  if ($1.weight) {
    if ($dstloc.player)
      $dstloc.load=$dstloc.load+$1.weight;
    
    if ($srcloc.player)
      $srcloc.load=$srcloc.load-$1.weight;
  }
  

  if ($srcloc.player)
  {
    if ($srcloc.inUse==$1)
    {
      $srcloc.inUse==NULL;
      $srcloc._use==NULL;
    }

    if ($srcloc.weapon==$1)
      $srcloc.weapon=bareHandsItem;
  }

  @.__move ($1,$2) ;

  if (($srcloc.player || $srcloc.mobile) && $1.armour)
    @._calcTotArmour($srcloc);
  if (($dstloc.player || $dstloc.mobile) && $1.armour)
    @._calcTotArmour($dstloc);

} chmod (&@._move,"W:rs") ;

func @.home
{
  if($# > 0) {
    if($1 == "here") {
      $new = #actor.location;
      if(objectowner($new) != #actor) {
	echo("you don't own this place!\n");
	return;
      }
      
      #actor.home = $new;
      return;
    }
    
  }

  echo("You click your heels together 3 times and mutter:\n",
       "  \"There's no place like home.\" ...\n") ;

  @._visEmote("clicks ", @._hisHer(#actor), " heels together 3 times and mutters:\n",
              "  \"There's no place like home.\" ...\n") ;

  if(#actor.home == NULL) {
    echo("You have no home!\n");
    return;
  }
  
  echo("Your surroundings fade, to be replaced with ...\n") ;  
  @._visEmote("fades into nothingness as ", @._heShe(#actor), " goes home...");
  @.__move(#actor, #0); /* Stash away for a while while we home the items! */
  if (!WIZ(#actor))
    @._resetInvent(#actor);
  @._moveply(#actor.home);
  @._visEmote("suddenly materialises before you!");
} chmod(&@.home,"W:rs");


func @._dirName			/* D	Get direction name		*/
{
  if ($1 == 0) return "north"       ;
  else if ($1 == 1) return "northeast"   ;
  else if ($1 == 2) return "east"        ;
  else if ($1 == 3) return "southeast"   ;
  else if ($1 == 4) return "south"       ;
  else if ($1 == 5) return "southwest"   ;
  else if ($1 == 6) return "west"        ;
  else if ($1 == 7) return "northwest"   ;
  else              return "**nowhere**" ;
}


func @._go			/* d	Move #actor in direction	*/
{
  if (#actor.paralysed)
  {
    echo ("The roots prevent you from going anywhere.\n") ;
    return ;
  }

  if (#actor.hasmoved)
    return;

  $followers=#actor.followers;

  $here = #actor.location ;
  $exit = match($1,$here.exits,"name") ;
  if ($exit == NULL)
  {
    echo (@.noGo) ;
    @._doFollowers ($here, $1, $followers);
    return ;
  }

  if (#actor.fighting)
  {
    echo("You're in the middle of a fight! You can only 'flee' from here!\n");
    @._doFollowers ($here, $1, $followers);
    return;
  }
  
  $to = $exit.destination ;
  if ($here._preExitFn ($to, $here) && !#actor._forceGo) return ;
  echo ("You go ",$exit.name,".\n") ;
  if (!#actor._leavingEmote)
    @._visEmote ("goes ", $exit.name,".\n") ;
  else
    @._visEmote(#actor._leavingEmote, " ", $exit.name, ".\n");

  if ($here._postExitFn ($to, $here) && !#actor._forceGo) return ;
  @.__move(#actor, $to) ;
  if ($to._preEnterFn ($here, $to) && !#actor._forceGo) return ;

  if (!#actor._enteringEmote)
    @._visEmote ("arrives.\n") ;
  else
    @._visEmote(#actor._enteringEmote, "\n");

  @._doFollowers($here, $1, $followers);

  if ($to._postEnterFn ($here, $to) && !#actor._forceGo) return ;
  @._quickLook() ;
} chmod(&@._go, "W:rs");

func @._doFollowers		/* XW	Move all of players followers	*/
{
  #actor.hasmoved=1;
  $here=$1;
  foreach $pl in ($3)
    if ($pl.location==$here)
      if (@._youCanSeeMe($pl)) {
        becomePlayer($pl);
        @._go($2);
        becomeMyself;
      }

  #actor.hasmoved=NULL;
  foreach $pl in (#actor.followers)
    $pl.hasmoved=NULL;
} chmod(&@._doFollowers, "W:rs");


func @.n { @._go ("north") ; }
func @.ne { @._go ("ne") ; }
func @.e { @._go ("east") ; }
func @.se { @._go ("se") ; }
func @.s { @._go ("south") ; }
func @.sw { @._go ("sw") ; }
func @.w { @._go ("west") ; }
func @.nw { @._go ("nw") ; }
func @.u { @._go ("up") ; }
func @.d { @._go ("d") ; }

func @.go			/* D	Help for silly people using go	*/
{
  echo("
Just type the direction you wish to travel. Don't type \"go\". Type
\"help go\" for a list of all possible directions, or use the \"exits\"
command to locate obvious exits from your current location. If you get
really stuck, you can type 'home' to take you back home, but you lose
all your possessions by doing this.\n");
}

func @.exits
{
  @._visEmote ("searches around for obvious exits.") ;
  $cnt = 0 ;
  foreach $exit in (#actor.location.exits) {
    if ($exit.hidden != 1) {
      if ($cnt == 0) echo ("Obvious exits: ") ;
      echo (" ", $exit.name) ;
      $cnt = 1 ;
    }
  }
  if ($cnt != 0) echo (".\n") ;
  if (#actor.level > 19) {
    $c2 = 0 ;
    foreach $exit in (#actor.location.exits) {
      if ($exit.hidden == 1) {
        if ($c2 == 0) echo ("Hidden exits: ") ;
        echo (" ", $exit.name) ;
        $c2 = 1 ; $cnt = 1 ;
      }
    }
    if ($c2 != 0) echo (".\n") ;
  }
  if ($cnt == 0) {
    echo ("You see no obvious way out of here.\n") ;
    return ;
  }
}

func @.follow
{
  minArgs(1, "*** follow whom?");
  bundleArgs;
  $who = @._singleVis(#actor.location.contents, $arg, @.noOneHere);
  if (!$who) return;
  if (!$who.player) {
    echo("You can only follow other players!\n");
    return;
  }

  $who.followers=listadd($who.followers, #actor);
  #actor.following=$who;

  if (@._youCanSeeMe($who)) {
    @._tellNAME($who, #actor);
    echoto($who, " has started following you.\n");
  }

  echo("You are now following ", $who.name, ".\n",
       "Type \'sf\' to stop following ", @._himHer($who), ".\n");
} chmod(&@.follow, "W:rs");

func @.sf
{
  if (#actor.following==NULL) {
    echo("You are not following anybody.\n");
    return;
  }

  echo("You have stopped following ", #actor.following.name, ".\n");

  #actor.following.followers=listdrop(#actor.following.followers, #actor);
  #actor.following=NULL;

} chmod(&@.sf, "W:rs");

